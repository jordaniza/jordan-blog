---
title: Fallback
description: The first challenge in Ethernaut, Fallback, is a great introduction to the EVM and how to write calls in Yul/Assembly
index: 2
prev: setup
---

## Topics Covered:

- Reading from storage slots
- Loading from memory
- Making contract calls in assembly

### Recap:

Recall our contract:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Fallback {
    mapping(address => uint256) public contributions;
    address public owner;

    constructor() {
        owner = msg.sender;
        contributions[msg.sender] = 1000 * (1 ether);
    }

    modifier onlyOwner() {
        require(msg.sender == owner, "caller is not the owner");
        _;
    }

    function contribute() public payable {
        require(msg.value < 0.001 ether);
        contributions[msg.sender] += msg.value;
        if (contributions[msg.sender] > contributions[owner]) {
            owner = msg.sender;
        }
    }

    function getContribution() public view returns (uint256) {
        return contributions[msg.sender];
    }

    function withdraw() public onlyOwner {
        payable(owner).transfer(address(this).balance);
    }

    receive() external payable {
        require(msg.value > 0 && contributions[msg.sender] > 0);
        owner = msg.sender;
    }
}
```

## The Exploit

The exploit here is reasonably straightforward. Take a second and see if you can spot it...

We're trying to become the `owner` so we can see that the owner is set in 3 places:

1. The constructor (we can discard this)
2. The contribute function
3. The fallback function

So we know that we will need to call some combination of the contribute and fallback function to become owner, after which point we can just withdraw the contract balance.

## Our first line of assembly

We're now ready to start writing yul. This exploit requires contract calls, so let's first see how do we make a low level call.

When speaking about the EVM, we are going to be making a call and need to determine a few things:

- The address to call
- The data to send to in the call, comprised of:
  - The function signature
  - The calldata
- The amount of gas to send
- Any wei to send with the call
- Any expected return data

### Fetching the address

Our address would be `address(target)` in solidity. However if we try and do this in Yul, it wont compile.

Instead we have to think in terms of how Yul thinks. Some core principles:

1. Declare inline assembly by using `assembly {}`
2. Variables can be declared inside assembly blocks using

```solidity
assembly {
  let variable := 1 // note we do not need semicolons in assembly
}
```

3. You can also declare variables outside of assembly blocks, and assign them inside such blocks.

_Be careful with this: there are no such thing as types inside assembly, in general a bytes32 type is a reasonably safe representation of the assembly variable._

Recall that we have defined our target in the contract state variables:

```solidity
contract ASMFallback is Test {
    Fallback public target;
```

This means that `target` is stored in the contract storage, in one of the storage `slots`. You might look at that and think "cool slot zero", but the fact we are inheriting from `Test` makes that a bit challenging to determine.

Fortunately, we have a special assembly property for this:

```solidity
assembly {
  // fetch the target address by loading the data stored at the storage slot of target
  // the variables are not written to memory automatically
  // but stored on the stack, then the compiler make it available when we need it
  let targetAddress  := sload(target.slot)
}
```

Let's do a quick log to see that this has worked correctly:

```solidity
// add this to our contract
function testFetchAddress() public view {
  bytes32 fetchedAddress;
  assembly {
    fetchedAddress := sload(target.slot)
  }
  console2.log("Fetched from assembly");
  console2.logBytes32(fetchedAddress);

  console2.log("Fetched from solidity");
  console2.log(address(target));
}
```

Now let's run it with the following command:

```sh
forge test --match-test testFetchAddress -vvvvv
```

Here is the output:

```
Running 1 test for test/Fallback.t.sol:ASMFallback
[PASS] testFetchAddress() (gas: 7028)
Logs:
  Fetched from assembly
  0x0000000000000000000000005615deb798bb3e4dfa0139dfa1b3d433cc23b72f
  Fetched from solidity
  0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f
```

So we can see that the address is in there, and crucially the rest of the 32 byte word is padded with extra zeroes.

This is a pretty crucial concept that we will need to remember when working with data in the EVM:

- Decoding data in the right way is _essential_ when working with EVM data.

Let's add a couple of steps to our logging, converting from a bytes32 value -> 32 byte/256 bit unsigned integer (uint256) -> 160 bit unsigned integer -> ethereum address (which is itself 160 bits):

```solidity
function testFetchAddress() public view {
  bytes32 fetchedAddress;
  assembly {
    fetchedAddress := sload(target.slot)
  }
  console2.log("Fetched from assembly");
  console2.logBytes32(fetchedAddress);

  console2.log("Converted to 256 bit uint");
  console2.log(uint(fetchedAddress));

  console2.log("Converted to 160 bit uint");
  console2.log(uint160(uint(fetchedAddress)));

  console2.log("Converted to address");
  console2.log(address(uint160(uint(fetchedAddress))));

  console2.log("Fetched from solidity");
  console2.log(address(target));
}
```

aaaand the output:

```
Fetched from assembly
0x0000000000000000000000005615deb798bb3e4dfa0139dfa1b3d433cc23b72f
Converted to 256 bit uint
491460923342184218035706888008750043977755113263
Converted to 160 bit uint
491460923342184218035706888008750043977755113263
Converted to address
0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f
Fetched from solidity
0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f
```

### The function signature

Define the function signature as 4 bytes (8 characters of hex) of the keccak256 hash of the function name, for example:

```js
// example function
function doStuff(address _to) public { /* */ }

// the function signature
"doStuff(address)"

// keccak256 hash of the function signature
"373cf23c710cb05bc335f291b632bd5850910a740614e32783c487b47f38ddc3"

// the first 4 bytes
"373cf23c"
```

If we were to look at the function loaded into a word on the calldata, we would get:

```
0x373cf23c0000000000000000000000008e851e94e1667Cd76Dda1A49f258934E2BCDCF3e
  --------                        ----------------------------------------
  signature                                        argument
```

Recall that, in solidity, we can make a low-level contract call in the following way:

```solidity
bytes memory data = abi.encodeWithSignature("doStuff(uint256)", (someNumber));

address(Contract).call({value: _value})(data);
```

In Yul, things are slightly different. Let's look at the super useful [EVM Codes](https://www.evm.codes/#f1?fork=homestead)

There's a lot there, but let's jump to just the parameters:

> Stack input
> gas: amount of gas to send to the sub context to execute. The gas that is not used by the sub context is returned to this one.
> address: the account which context to execute.
> value: value in wei to send to the account.
> argsOffset: byte offset in the memory in bytes, the calldata of the sub context.
> argsSize: byte size to copy (size of the calldata).
> retOffset: byte offset in the memory in bytes, where to store the return data of the sub context.
> retSize: byte size to copy (size of the return data).

### Answer

1. Call contribute with 1 wei to pass the require check in the fallbak.
2. Call fallback with 1 wei to become the owner
3. Call withdraw to extrac the balance of the contract.
